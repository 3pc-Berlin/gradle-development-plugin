/**
 *  Version Plugin for Semantic Versioning controlled by git tags.
 *
 *  Source: https://github.com/ilovemilk/semver-git-plugin
 */

buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath("io.wusa:semver-git-plugin:2.3.7")
}

apply plugin: io.wusa.SemverGitPlugin

semver {
    tagType = 'LIGHTWEIGHT'
    snapshotSuffix = ""
    dirtyMarker = "dirty" // show uncommitted files
    initialVersion = "0.0.1"

    branches { // list of branch configurations
        branch {
            regex = "release/.*"
            incrementer = "NO_VERSION_INCREMENTER"
            formatter = { "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}-rc.${semver.info.count}" }
        }
        branch {
            regex = "master"
            incrementer = "NO_VERSION_INCREMENTER"
            formatter = { "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}" }
        }
        branch {
            regex = "develop" // DEFAULT: [develop, feature, etc.]
            incrementer = "NO_VERSION_INCREMENTER"
            formatter = { "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}-dev.${semver.info.count}" }
        }
        branch {
            regex = ".+" // regex for the branch you want to configure, put this one last
            incrementer = "NO_VERSION_INCREMENTER"
            formatter = { "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}-build.${semver.info.count}.sha.${semver.info.shortCommit}" }
        }
    }
}

allprojects {
    project.version = System.getenv("CI_APP_VERSION") ?: semver.info.toString()    // allow CI to override version (to standardize versioning scheme)
}